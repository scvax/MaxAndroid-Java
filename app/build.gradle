apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "max.android.template"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        flavorDimensions "versionCode"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix "-debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    dexOptions {
        preDexLibraries = false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    productFlavors {
        dev {
            resValue('string', 'app_name', 'MaxAndroid-开发版')
            buildConfigField "String", "SERVER_BASE", '"http://192.168.0.101:8888/"'
        }
        beta {
            resValue('string', 'app_name', 'MaxAndroid-测试版')
            buildConfigField "String", "SERVER_BASE", '"http://10.156.47.39:8888/"'
        }
        pre {
            resValue('string', 'app_name', 'MaxAndroid')
            buildConfigField "String", "SERVER_BASE", '"http://max.android.com/"'
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.productFlavors[0].name.endsWith("dev")) {
                outputFileName = "app_dev_v${defaultConfig.versionName}_${defaultConfig.versionCode}_${buildTime()}.apk"
            } else if (variant.productFlavors[0].name.endsWith("beta")) {
                outputFileName = "app_beta_v${defaultConfig.versionName}_${defaultConfig.versionCode}_${buildTime()}.apk"
            } else if (variant.productFlavors[0].name.endsWith("pre")) {
                outputFileName = "app_pre_v${defaultConfig.versionName}_${defaultConfig.versionCode}_${buildTime()}.apk"
            }
        }
    }
}

static def buildTime() {
    return new Date().format("yyyy-MM-dd-HH-mm-ss", TimeZone.getTimeZone("GMT+08:00"))
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    // androidx
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    // 日志打印
    implementation 'com.orhanobut:logger:2.2.0'
    // JSON解析
    implementation 'com.google.code.gson:gson:2.8.6'
    // 网络访问
    implementation 'com.squareup.okhttp3:okhttp:4.1.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.1.0'
    implementation 'com.squareup.retrofit2:retrofit:2.6.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.1'
    // 异步任务
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    // banner
    implementation 'com.youth.banner:banner:2.0.10'
    // RecyclerViewAdapter
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.3'
    // 小红点
    implementation 'q.rorbin:badgeview:1.1.3'
    // 图片加载
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    // 圆形图片
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    // 下拉刷新
    implementation 'com.scwang.smart:refresh-layout-kernel:2.0.0'
    implementation 'com.scwang.smart:refresh-header-material:2.0.0'
    implementation 'com.scwang.smart:refresh-footer-classics:2.0.0'
}
